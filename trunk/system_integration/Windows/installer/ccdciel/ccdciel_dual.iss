; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
 
[Setup]
AppName=CCDciel
AppVerName=CCDciel V3
AppPublisherURL=http://sourceforge.net/projects/ccdciel
AppSupportURL=http://sourceforge.net/projects/ccdciel
AppUpdatesURL=http://sourceforge.net/projects/ccdciel
UsePreviousAppDir=true
DefaultDirName={pf}\CCDciel
DefaultGroupName=CCDciel
AllowNoIcons=true
InfoBeforeFile=Presetup\readme.txt
OutputDir=.\
OutputBaseFilename=ccdciel-windows
Compression=lzma
SolidCompression=true
Uninstallable=true
UninstallLogMode=append
DirExistsWarning=no
ShowLanguageDialog=yes
AppID={{6570df38-f18f-11e4-9532-fb2d36b55e00}
ArchitecturesInstallIn64BitMode=x64

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}

[Files]
Source: "Prog\ccdciel-x64.exe"; DestDir: "{app}"; DestName: "ccdciel.exe"; Check: Is64BitInstallMode
Source: "Prog\libccdcielwcs-x64.dll"; DestDir: "{app}"; DestName: "libccdcielwcs.dll"; Check: Is64BitInstallMode
Source: "Prog\ccdciel.exe"; DestDir: "{app}"; Check: not Is64BitInstallMode
Source: "Prog\libccdcielwcs.dll"; DestDir: "{app}"; Check: not Is64BitInstallMode
Source: Data\*; DestDir: {app}; Flags: ignoreversion recursesubdirs createallsubdirs restartreplace
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: {group}\CCDciel; Filename: {app}\ccdciel.exe; WorkingDir: {app}
Name: {userdesktop}\CCDciel; Filename: {app}\ccdciel.exe; WorkingDir: {app}; Tasks: desktopicon
 
[Code]
{Uninstall 32bit version on 64bit system}
function GetUninstallString: string;
var
  sUnInstPath: string;
  sUnInstallString: String;
begin
  Result := '';
  sUnInstPath := ExpandConstant('Software\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\{{6570df38-f18f-11e4-9532-fb2d36b55e00}_is1'); 
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;

function IsUpgrade: Boolean;
begin
  Result := (GetUninstallString() <> '');
end;

function InitializeSetup: Boolean;
var
  V: Integer;
  iResultCode: Integer;
  sUnInstallString: string;
begin
  Result := True; { in case when no previous version is found }
  if RegValueExists(HKEY_LOCAL_MACHINE,'Software\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\{6570df38-f18f-11e4-9532-fb2d36b55e00}_is1', 'UninstallString') then  
  begin
    V := MsgBox(ExpandConstant('A previous 32bit version of CCDciel was detected. It must be uninstalled before the 64bit version can be installed. All your configuration setting will be preserved. Do you want to continue?'), mbInformation, MB_YESNO); { Custom Message if App installed }
    if V = IDYES then
    begin
      sUnInstallString := GetUninstallString();
      sUnInstallString :=  RemoveQuotes(sUnInstallString);
      Exec(ExpandConstant(sUnInstallString), '/SILENT', '', SW_SHOW, ewWaitUntilTerminated, iResultCode);
      Result := True; { if you want to proceed after uninstall }
      { Exit; //if you want to quit after uninstall }
    end
    else
      Result := False; { when older version present and not uninstalled }
  end;
end;
